buildscript {
    repositories {
        jcenter()
        mavenCentral()
    }
    dependencies {
        //does not work if added as new plugin property
        classpath "com.github.dcendents:android-maven-gradle-plugin:1.3"
    }
}
plugins {
    id "com.jfrog.bintray" version "1.4"
}

apply plugin: 'java'
apply plugin: 'groovy'
apply plugin: "com.github.dcendents.android-maven"

group = 'com.vdragutinovic.gradle'
version = '1.5'

task wrapper(type: Wrapper) {
    gradleVersion = '2.4'
}

def gitUrl = 'https://github.com/massa89/gradle-advanced-build-version.git'
def siteUrl = 'https://github.com/massa89/gradle-advanced-build-version'

install {
    repositories.mavenInstaller {
        // This generates POM.xml with proper parameters
        pom {
            project {
                packaging 'jar'

                // Add your description here
                name 'Gradle plugin for auto versioning'
                url siteUrl

                // Set your license
                licenses {
                    license {
                        name 'The Apache Software License, Version 2.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                scm {
                    connection gitUrl
                    developerConnection gitUrl
                    url siteUrl

                }
            }
        }
    }
}

bintray {
    user = project.hasProperty('bintrayUser') ? project.property('bintrayUser') : System.getenv('BINTRAY_USER')
    key = project.hasProperty('bintrayApiKey') ? project.property('bintrayApiKey') : System.getenv('BINTRAY_API_KEY')
    configurations = ['archives']
    pkg {
        repo = 'maven'
        name = 'gradle-advanced-build-version'
        userOrg = user
        licenses = ['Apache-2.0']
        vcsUrl = 'https://github.com/massa89/gradle-advanced-build-version.git'
        version {
            name = '1.5'
            desc = 'Advanced versioning 1.5'
            released  = new Date()
            vcsTag = '1.5'
        }
    }
}

repositories {
    mavenCentral()
}

dependencies {
    compile gradleApi()
    compile localGroovy()
    compile 'com.android.tools.build:gradle:0.14.0'
    testCompile 'org.easytesting:fest-assert:1.4'
}

test {
    useTestNG()
}
